apply from: '../service.gradle'

dependencies {
    compile 'com.datastax.cassandra:cassandra-driver-core:3.3.0'
    compile 'io.netty:netty-all:4.1.6.Final'
    compile 'org.springframework.data:spring-data-cassandra'
    compile 'org.springframework.boot:spring-boot-configuration-processor'
    testCompile 'org.cassandraunit:cassandra-unit-spring:3.3.0.2'
}

task bootRunDebugWithCassandra(type: org.springframework.boot.gradle.run.BootRunTask) {
    group = 'Application'
    doFirst() {
        main = project.mainClassName
        classpath = sourceSets.main.runtimeClasspath

        def serviceinfos = dockerCompose.debugWithCassandra.servicesInfos
        setCassandraEnv(serviceinfos, environment)
    }
}

apply plugin: 'docker-compose'

dockerCompose {
    debugWithCassandra {
        useComposeFiles = ['docker-compose-debug.yml']
        isRequiredBy(this.tasks.getByName('bootRunDebugWithCassandra'))
    }
    integrationTestWithCassandra {
        useComposeFiles = ['docker-compose-integrationtest.yml']
        isRequiredBy(this.tasks.getByName('integrationTest'))
        removeContainers = true
    }
}

integrationTest.doFirst {
    def serviceinfos = dockerCompose.integrationTestWithCassandra.servicesInfos
    setCassandraEnv(serviceinfos, environment)
}

def setCassandraEnv(serviceinfos, environment) {
    def cassandrainfos = serviceinfos.cassandra
    def cassandrahost = cassandrainfos.firstContainer.inspection.NetworkSettings.Networks.entrySet().iterator().next().getValue().IPAddress
    def cassandraport = cassandrainfos.ports[9042]

    environment.put('cassandra.host', cassandrahost + ',' + cassandrainfos.host)
    environment.put('cassandra.port', cassandraport)
    environment.put('cassandra.keyspace', 'test')
}